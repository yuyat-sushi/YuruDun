BattleLoad:
        ;敵HP・ATK・AS設定
        xor a
        ld      [b_EnemyHP],a
        ld      [b_EnemyATK],a
        ld      a,30
        ld      [b_EnemyHP+1],a
        ld      a,5
        ld      [b_EnemyATK+1],a
        ld      a,60
        ld      [b_EnemyAS],a

        ;ASWait初期化
        ld      a,[b_PlayerAS]
        ld      [b_PlayerASWait],a
        ld      a,[b_EnemyAS]
        ld      [b_EnemyASWait],a
        AdressLD	BattleAwake, _SceneAdress
        ret 

BattleAwake:
        ;グラフィックロード
        ;VRAM操作するため、スクリーンをオフに
        call    ScreenOff
        ;共通データ
	ld      bc, 32*16
        ld      de, _VRAM + $1000
        ld      hl, _TextTile
        call    LDIR
        ;背景データ
	ld      bc, 14*16
        ld      de, _VRAM + $1200
        ld      hl, _BattleFieldTile
        call    LDIR
        ;敵
        ld      bc, 24*16
        ld      de, _VRAM + $1600
        ld      hl, _EnemyTile1
        call    LDIR
        ;背景マップ
        ld      bc,$140A
        ld      de,_BattleFieldMap
        ld      hl,_SCRN0
        call    g_SetTileMap
        ;ステータスマップ
        ld      bc,$1408
        ld      de,_BattleStatusMap
        ld      hl,_SCRN0 + $140
        call    g_SetTileMap
        ;敵マップ
        ld      bc,$0604
        ld      de,_Enemy1Map
        ld      hl,_SCRN0 + $C2
        call    g_SetTileMap
        call    __SetParameter
        ;描画処理終了
        call    ScreenOnVB
        AdressLD	BattleUpdate, _SceneAdress
        AdressLD        BattleVblank, _VblankAdress
        ret

BattleUpdate:
        ;バトル処理
        ;自分の攻撃
        ;ASWait判定
        ld      a,[b_PlayerASWait]
        cp      0
        jr      nz,.PlayerASCountDown
        ;ASWaitが0になった時、相手HP-自ATK
        ld      a,[b_EnemyHP]
        ld      h,a
        ld      a,[b_EnemyHP+1]
        ld      l,a

        ld      a,[b_PlayerATK]
        xor     $FF
        ld      b,a
        ld      a,[b_PlayerATK+1]
        xor     $FF
        ld      c,a
        inc     bc

        add     hl,bc
        
        ld      a,h
        ld      [b_EnemyHP],a
        ld      a,l
        ld      [b_EnemyHP+1],a

        ;ASWait初期化
        ld      a,[b_PlayerAS]
        ld      [b_PlayerASWait],a        

        jr      .endPlayerBattle
.PlayerASCountDown:
        ld      hl,b_PlayerASWait
        dec     [hl]
.endPlayerBattle:
        ;相手の攻撃
        ;ASWait判定
        ld      a,[b_EnemyASWait]
        cp      0
        jr      nz,.EnemyASCountDown
        ;ASWaitが0になった時、自HP-相手ATK
        ld      a,[b_PlayerHP]
        ld      h,a
        ld      a,[b_PlayerHP+1]
        ld      l,a

        ld      a,[b_EnemyATK]
        xor     $FF
        ld      b,a
        ld      a,[b_EnemyATK+1]
        xor     $FF
        ld      c,a
        inc     bc

        add     hl,bc
        
        ld      a,h
        ld      [b_PlayerHP],a
        ld      a,l
        ld      [b_PlayerHP+1],a

        ;ASWait初期化
        ld      a,[b_EnemyAS]
        ld      [b_EnemyASWait],a        

        jr      .endEnemyBattle
.EnemyASCountDown:
        ld      hl,b_EnemyASWait
        dec     [hl]
.endEnemyBattle:

        ;文字表示の前準備
        call    __SetParameter
        ret

BattleVblank:
        ;文字表示
        ld      bc,$0503
        ld      de,b_PlayerParameterBuffer
        ld      hl,_SCRN0 + $1C4
        call    g_SetTileMap
        ld      bc,$0503
        ld      de,b_EnemyParameterBuffer
        ld      hl,_SCRN0 + $1CE
        call    g_SetTileMap
        ret

__SetParameter:
        ;味方のパラメーター表示
        ld      a,[b_PlayerHP]
        ld      b,a
        ld      a,[b_PlayerHP+1]
        ld      c,a
        ld      d,$10
        ld      hl,b_PlayerParameterBuffer
        call    g_Set5BCDBG
        ld      a,[b_PlayerATK]
        ld      b,a
        ld      a,[b_PlayerATK+1]
        ld      c,a
        ld      d,$10
        ld      hl,b_PlayerParameterBuffer + 5
        call    g_Set5BCDBG
        ld      a,[b_PlayerAS]
        ld      c,a
        ld      d,$10
        ld      hl,b_PlayerParameterBuffer + 12
        call    g_Set3BCDBG
        ;敵のパラメーター表示
        ld      a,[b_EnemyHP]
        ld      b,a
        ld      a,[b_EnemyHP+1]
        ld      c,a
        ld      d,$10
        ld      hl,b_EnemyParameterBuffer
        call    g_Set5BCDBG
        ld      a,[b_EnemyATK]
        ld      b,a
        ld      a,[b_EnemyATK+1]
        ld      c,a
        ld      d,$10
        ld      hl,b_EnemyParameterBuffer + 5
        call    g_Set5BCDBG
        ld      a,[b_EnemyAS]
        ld      c,a
        ld      d,$10
        ld      hl,b_EnemyParameterBuffer + 12
        call    g_Set3BCDBG
        ret